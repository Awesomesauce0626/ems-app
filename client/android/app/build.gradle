apply plugin: 'com.android.application'

// Define a task to copy web assets from the /dist folder to the Android assets folder.
task copyWebApp(type: Copy) {
    from "../../dist"
    into "src/main/assets/public"
    doFirst {
        println ">>> EXECUTING copyWebApp: Copying web app files..."
    }
}

// It makes the standard asset and lint tasks dependent on our copy task.
tasks.whenTaskAdded {
    if (it.name == 'mergeReleaseAssets' || it.name == 'lintVitalAnalyzeRelease' || it.name == 'generateReleaseLintVitalReportModel') {
        it.dependsOn copyWebApp
    }
}

def localProperties = new Properties()
try {
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))
} catch (IOException e) {
    // local.properties not found, reasonable in CI environments
}

android {
    signingConfigs {
        release {
            storeFile file('../../../new-upload-key.jks') // Use the new upload keystore
            storePassword SIGNING_STORE_PASSWORD
            keyAlias SIGNING_KEY_ALIAS
            keyPassword SIGNING_KEY_PASSWORD
        }
    }

    namespace "com.prc_ems.app"
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.prc_ems.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 19
        versionName "1.19"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
        multiDexEnabled true

        buildConfigField "String", "CLOUDINARY_CLOUD_NAME", "\"${localProperties.getProperty('cloudinaryCloudName', '')}\""
        buildConfigField "String", "CLOUDINARY_API_KEY", "\"${localProperties.getProperty('cloudinaryApiKey', '')}\""
        buildConfigField "String", "CLOUDINARY_API_SECRET", "\"${localProperties.getProperty('cloudinaryApiSecret', '')}\""
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release // Apply the release signing config
        }
    }
    buildFeatures {
        buildConfig true
    }
}

// Repositories are now managed in settings.gradle

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    implementation project(':capacitor-push-notifications')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')

    implementation "androidx.multidex:multidex:2.0.1"

    // Firebase (Auth & Firestore)
    implementation(platform("com.google.firebase:firebase-bom:33.0.0"))
    implementation("com.google.firebase:firebase-auth-ktx")
    implementation("com.google.firebase:firebase-firestore-ktx")

    // Maps (OpenStreetMap - No Credit Card Needed!)
    implementation("org.osmdroid:osmdroid-android:6.1.18")

    // Cloudinary for Photos/Videos
    // implementation("com.cloudinary:cloudinary-android:3.0.0")

    // Retrofit for API calls
    implementation("com.squareup.retrofit2:retrofit:2.9.0")
    implementation("com.squareup.retrofit2:converter-gson:2.9.0")
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
